apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'de.mannodermaus.android-junit5'

android {
  dexOptions {
    javaMaxHeapSize '4g'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    compileSdkVersion rootProject.ext.sdkVersionInformation.compileSdkVersion
    minSdkVersion rootProject.ext.sdkVersionInformation.minSdkVersion
    targetSdkVersion rootProject.ext.sdkVersionInformation.targetSdkVersion

    buildToolsVersion rootProject.ext.miscVersionInformation.androidBuildTools

    versionCode rootProject.ext.appVersionInformation.versionCode
    versionName ''

    multiDexEnabled true

    proguardFile 'proguard-rules.pro'

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    dev {
      versionNameSuffix rootProject.ext.appVersionInformation.devVersionName
    }

    internalTesting {
      versionNameSuffix rootProject.ext.appVersionInformation.internalTestVersionName
    }

    externalTesting {
      versionNameSuffix rootProject.ext.appVersionInformation.externalTestingVersionName
    }

    prod {
      versionNameSuffix rootProject.ext.appVersionInformation.prodVersionName
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
}

apply from: rootProject.file('dependencies.gradle')

dependencies {
  api project.ext.dependencies.androidSupport.appCompat
  api project.ext.dependencies.androidSupport.design

  api project.ext.dependencies.rxJava

  implementation project.ext.dependencies.rxKotlin

  api project.ext.dependencies.jodaTime

  api project.ext.dependencies.gson

  implementation project.ext.dependencies.guava

  implementation project.ext.dependencies.apacheCommons.io

  api (project.ext.dependencies.materialSearchView) {
    exclude group: 'com.android.support'
  }

  implementation project.ext.dependencies.kotlinStdLib

  compileOnly project.ext.dependencies.autoValue.annotations
  implementation project.ext.dependencies.autoValue.gsonPluginRuntime
  annotationProcessor project.ext.dependencies.autoValue.compiler
  annotationProcessor project.ext.dependencies.autoValue.gsonPluginCompiler

  testImplementation junit5.unitTests()
  testImplementation junit5.parameterized()

  testImplementation project.ext.dependencies.mockito

  testImplementation project.ext.dependencies.assertk

  androidTestImplementation junit5.instrumentationTests()

  androidTestImplementation project.ext.dependencies.dexmaker.core
  androidTestImplementation project.ext.dependencies.dexmaker.mockitoPlugin

  androidTestImplementation project.ext.dependencies.androidTestingSupport.espresso.core
}

task deleteOutputDirectories {
  delete('build')
  delete('out')
}